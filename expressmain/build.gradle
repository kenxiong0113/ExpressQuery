apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
android {
    compileSdkVersion compile_sdk_version.toInteger()
    defaultConfig {
        applicationId "com.ken.expressquery"
        minSdkVersion min_sdk_version.toInteger()
        targetSdkVersion target_sdt_version.toInteger()
        versionCode 7
        versionName "1.1.7"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }

    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion "$support_version"
                }
            }
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation fileTree(dir: 'libs', include: ['*.aar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
    implementation project(':base')
    runtimeOnly project(':login')
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.ashokvarma.android:bottom-navigation-bar:1.3.0'
    implementation 'com.githang:status-bar-compat:latest.integration'
}
greendao {
    schemaVersion 9
    daoPackage 'com.ken.expressquery.greendao'
    targetGenDir 'src/main/java'
}
//检查api过时版本
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

// 依赖插件脚本
apply from: 'tinker-support.gradle'
